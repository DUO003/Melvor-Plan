```python


extends Node

const PORT = 4433

func _ready():
	# Start paused
	
	#- 2D 和 3D 物理将停止，包括信号和碰撞检测。
	get_tree().paused = true
	# You can save bandwith by disabling server relay and peer notifications.
	#不知道为啥要禁用对等体通知
	multiplayer.server_relay = false

	#判断是否是无头模式,是的话就自动启动
	if DisplayServer.get_name() == "headless":
		print("无头模式自动启动服务器吗,运行创建服务器函数")
		_on_host_pressed.call_deferred()


func _on_host_pressed():
	
	#创建服务器
	var peer = ENetMultiplayerPeer.new()
	peer.create_server(PORT)
	
	#返回连接状态
	#该 MultiplayerPeer 已断开连接。
	if peer.get_connection_status() == MultiplayerPeer.CONNECTION_DISCONNECTED:
		OS.alert("启动服务器失败")
		return
	#关联到MultiplayerAPI实例
	multiplayer.multiplayer_peer = peer
	#创建成功就启动游戏
	start_game()


func _on_connect_pressed():
	#连接服务器
	# Start as client
	var txt : String = $UI/Net/Options/Remote.text
	if txt == "":
		OS.alert("需要端口.")
		return
	#创建一个客户端提
	var peer = ENetMultiplayerPeer.new()
	peer.create_client(txt, PORT)
	if peer.get_connection_status() == MultiplayerPeer.CONNECTION_DISCONNECTED:
		#该 MultiplayerPeer 已断开连接。
		OS.alert("不能连接到多人服务器")
		return
	#串联
	multiplayer.multiplayer_peer = peer
	start_game()

func start_game():
	# 隐藏UI
	$UI.hide()
	#取消暂停游戏
	get_tree().paused = false
	#仅更改服务器上的级别。
	#客户端将通过派生程序实例化级别。
	if multiplayer.is_server():
		#判断是否是服务器
		change_level.call_deferred(load("res://level.tscn"))


# Call this function deferred and only on the main authority (server).
func change_level(scene: PackedScene):
	# Remove old level if any.
	var level = $Level
	for c in level.get_children():
		#返回对节点的子节点的引用数组。
		
		#删除子节点
		level.remove_child(c)
		#删除节点?
		c.queue_free()
	
	#添加到节点	
	level.add_child(scene.instantiate())

# The server can restart the level by pressing HOME.
func _input(event):
	#拦截不是服务器的? 进入世界
	if not multiplayer.is_server():
		return
	if event.is_action("ui_home") and Input.is_action_just_pressed("ui_home"):
		change_level.call_deferred(load("res://level.tscn"))
